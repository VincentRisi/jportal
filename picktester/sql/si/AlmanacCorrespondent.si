DATABASE bbd FLAGS 'user=USId(16)' 'when=TmStamp'//
PACKAGE  bbd
OUTPUT   AlmanacCorrespondent
SERVER   bbd

TABLE AlmanacCorrespondent
    OPTIONS "lookup=FinId CorrCurrency CorrFinId CorrBranchId CorrSwiftAddress Status" "descr=Almanac Correspondents"
  FinId            int
  CorrCurrency     Char(4)
  CorrFinId        int
  CorrBranchId     int
  CorrBankName     Char(504)  NULL
  CorrTown         Char(70)   NULL
  CorrCountry      Char(100)  NULL
  CorrAccountNo    Char(50)   NULL
  CorrSwiftAddress Char(11)   NULL
  PreferredInd     Char(1)    NULL
  RBIInsertDate    DateTime   NULL
  RBIChangeDate    DateTime   NULL
  Status           byte     (Active=0 Inactive=1 MarkForDelete=2 ConfirmDelete=3)
  USId             Char(16)
  TmStamp          timestamp

KEY BankFileKey PRIMARY
  FinId
  CorrCurrency
  CorrFinId
  CorrBranchId

link Bankfile (SwiftAddress)
  CorrSwiftAddress

PROC Insert
PROC Update
PROC SelectOne
PROC DeleteOne
PROC Exists

// used by almanacto
PROC UpdateAllStatus
INPUT
  Status        byte
  USId          char  (16)
sqlcode
  Update
    AlmanacCorrespondent
  Set
      Status = :Status
     ,USId = :USId
     ,TMStamp = current_timestamp
endcode

//Used By Router, IDE
PROC Get
INPUT
   FinId            int
   CorrCurrency     Char(4)
OUTPUT (Single)
   CorrFinId        int
   CorrBankName     Char(504)
   CorrTown         Char(70)
   CorrCountry      Char(100)
   CorrAccountNo    Char(50)
   CorrSwiftAddress Char(11)
   PreferredInd     Char(1)
   Status           byte     // (Active=0 Inactive=1 MarkForDelete=2)
sqlcode
  Select
   ac.CorrFinId
  ,ac.CorrBankName
  ,ac.CorrTown
  ,ac.CorrCountry
  ,ac.CorrAccountNo
  ,ac.CorrSwiftAddress
  ,ac.PreferredInd
  ,ac.Status
  From
     AlmanacCorrespondent ac
  Where
     ac.FinId = :FinId
 and ac.CorrCurrency = :CorrCurrency
endcode

//Used By Router, IDE
PROC GetSwiftList
INPUT
   FinId            int
   CorrCurrency     Char(4)
OUTPUT
   CorrSwiftAddress Char(11)
   Status           byte     // (Active=0 Inactive=1 MarkForDelete=2)
sqlcode
  Select
  ac.CorrSwiftAddress
  ,ac.Status
  From
     AlmanacCorrespondent ac
  Where
     ac.FinId = :FinId
 and ac.CorrCurrency = :CorrCurrency
 and (not ac.CorrSwiftAddress is null)
endcode
